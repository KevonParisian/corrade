# Author: mosra <mosra@centrum.cz>
pkgname=corrade
pkgver=dev
pkgrel=1
pkgdesc="C++11/C++14 multiplatform utility library (coverage build)"
arch=('i686' 'x86_64')
url="https://magnum.graphics/corrade/"
license=('MIT')
makedepends=('cmake' 'ninja' 'lcov')
options=(!strip)
provides=('corrade-git')

_rootdir=$startdir/../../

build() {
    mkdir -p "$_rootdir/build-coverage"
    cd "$_rootdir/build-coverage"

    # Colored output is enabled implicitly. If Ninja detects it's outputting to
    # a pipe, it strips it away from the output, alternatively you can set the
    # GCC_COLORS= (empty) env variable to temporarily disable colors. The
    # inverse, i.e. preserving colors when Ninja outputs to a pipe can be done
    # with CLICOLOR_FORCE=1: https://github.com/ninja-build/ninja/issues/2196
    cmake .. \
        -DCMAKE_CXX_FLAGS="--coverage" \
        -DCMAKE_BUILD_TYPE=Debug \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_COLOR_DIAGNOSTICS=ON \
        -DCORRADE_BUILD_TESTS=ON \
        -G Ninja
    ninja
}

check() {
    export CORRADE_TEST_COLOR=ON

    cd "$_rootdir/build-coverage"
    # The Tester test *needs* benchmarks enabled to pass properly
    ctest --output-on-failure -R TestSuiteTesterTest || true
    CORRADE_TEST_SKIP_BENCHMARKS=ON ctest --output-on-failure -j9 -E TestSuiteTesterTest || true

    rm -rf coverage
    mkdir coverage
    # Keep in sync with package/ci/appveyor-lcov.sh and circleci.yml, please
    # TODO figure out a way to avoid adding --ignore-errors mismatch
    lcov --ignore-errors mismatch --directory . --capture --output-file coverage.info
    lcov --extract coverage.info "*/src/Corrade/*" --output-file coverage.info
    lcov --remove coverage.info "*/Test/*" --output-file coverage.info
    genhtml --output-directory ./coverage coverage.info
}

package() {
    echo "Open file://$_rootdir/build-coverage/coverage/index.html to see the results." && false
}
