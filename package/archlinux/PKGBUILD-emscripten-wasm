# Author: mosra <mosra@centrum.cz>
pkgname=emscripten-corrade
pkgver=dev.wasm
pkgrel=1
pkgdesc="C++11/C++14 multiplatform utility library (Emscripten, wasm)"
arch=('any')
url="https://magnum.graphics/corrade/"
license=('MIT')
makedepends=('cmake' 'emscripten' 'ninja')
options=(!strip !buildflags)

_rootdir=$startdir/../../

build() {
    if [ ! -d "$_rootdir/build-emscripten-wasm" ] ; then
        mkdir "$_rootdir/build-emscripten-wasm"
        cd "$_rootdir/build-emscripten-wasm"

        cmake .. \
            -DCMAKE_TOOLCHAIN_FILE="$_rootdir/toolchains/generic/Emscripten-wasm.cmake" \
            -G Ninja
    fi

    cd "$_rootdir/build-emscripten-wasm"

    # Put tests into a separate target as they take ages to build. They're
    # built only inside check() so `makepkg --nocheck` will skip building them.
    #
    # Colored output is enabled implicitly. If Ninja detects it's outputting to
    # a pipe, it strips it away from the output; Clang unfortunately doesn't
    # have any env var to temporarily disable colors. The inverse, i.e.
    # preserving colors when Ninja outputs to a pipe can be done with
    # CLICOLOR_FORCE=1: https://github.com/ninja-build/ninja/issues/2196
    cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr/lib/emscripten/system \
        -DCMAKE_COLOR_DIAGNOSTICS=ON \
        -DCORRADE_BUILD_TESTS=ON \
        -DCORRADE_TESTSUITE_TEST_TARGET=build-tests
    ninja
}

check() {
    cd "$_rootdir/build-emscripten-wasm"

    ninja build-tests

    CORRADE_TEST_COLOR=ON ctest --output-on-failure
}

package() {
    cd "$_rootdir/build-emscripten-wasm"
    DESTDIR="$pkgdir/" ninja install
}
